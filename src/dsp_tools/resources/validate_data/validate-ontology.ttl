# TODO: implement as SHACL




    # THis checks that seqnum / isPartOf has a max cardinality of 1

    """
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX knora-api: <http://api.knora.org/ontology/knora-api/v2#>

SELECT * WHERE {
    ?targetClass a owl:Class ;
                 rdfs:subClassOf ?restriction .
    ?restriction a owl:Restriction ;
                 owl:onProperty ?propName .

    { ?propName rdfs:subPropertyOf* knora-api:isPartOf }
    UNION
    { ?propName rdfs:subPropertyOf* knora-api:seqnum  }

    FILTER NOT EXISTS {
        ?restriction owl:maxCardinality|owl:cardinality 1
    }
}

"""


    # This checks that if a restriction for isPartOf exists one for seqnum does too

    """
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX knora-api: <http://api.knora.org/ontology/knora-api/v2#>

SELECT * WHERE {

    ?targetClass a owl:Class ;
    			       rdfs:subClassOf ?restrictionisPartOf .
    ?restrictionisPartOf a owl:Restriction ;
    					           owl:onProperty ?isPartOfProp .
    ?isPartOfProp rdfs:subPropertyOf* knora-api:isPartOf .

    FILTER NOT EXISTS {
      ?targetClass a owl:Class ;
                   rdfs:subClassOf ?restrictionSeqnum .
      ?restrictionSeqnum a owl:Restriction ;
                         owl:onProperty ?seqnumProp .
      ?seqnumProp rdfs:subPropertyOf* knora-api:seqnum .
    }
}
"""

        # This checks that if a restriction for seqnum exists one for isPartOf does too

    """
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX knora-api: <http://api.knora.org/ontology/knora-api/v2#>

SELECT * WHERE {

    ?targetClass a owl:Class ;
                 rdfs:subClassOf ?restrictionSeqnum .
    ?restrictionSeqnum a owl:Restriction ;
                       owl:onProperty ?seqnumProp .
    ?seqnumProp rdfs:subPropertyOf* knora-api:seqnum .

    FILTER NOT EXISTS {
      ?targetClass a owl:Class ;
                   rdfs:subClassOf ?restrictionisPartOf .
      ?restrictionisPartOf a owl:Restriction ;
                           owl:onProperty ?isPartOfProp .
      ?isPartOfProp rdfs:subPropertyOf* knora-api:isPartOf .
    }
}
"""